# ferret_paths_template    11/91 & 4/92
# Template for setting up the FERRET environment
# updated for FERRET version 3 2/93 
# Mod 10.93 *jd* fix for SunOS LD_LIBRARY_PATH
# Mod 03.94 *jd* for XGKS - remove reference to ATCgks
# Mod 05.97 *jd* add AIX-required environment var XLFRTEOPTS
# *jd* 4.27.99  - add FER_WEB_BROWSER
# *jd* 4.27.99  - add FER_EXTERNAL_FUNCTIONS
# *kob* 5.99 - change dir for FER_EXTERNAL_FUNCTIONS

#
# If you are installing FERRET for the first time at your site we ask
# that you please notify us with an Internat mail message to
# ferret@noaapmel.gov [192.68.161.0] (or call Steve Hankin at (206)526-6080)
# - no strings attached.  Please specify if you would like to be on a mailing
# list for FERRET updates.
#
# Modify this to suit your system and place the result as file `ferret_paths'
# in another directory set up to hold login initialization scripts.
# `/usr/local' is the suggested choice.
# Users of FERRET should include the command 
#  `source /usr/local/ferret_paths'
# (or wherever you have located the ferret_paths file) in their .login file.
#
# The environment variable FER_DIR should be the pathname of
# the directory named `ferret' you created to install the FERRET software.

# You may want to customize the pathname of the `ferret' directory:
	 setenv FER_DIR /home/fiedler/linux/ferret

# The environment variable FER_DSETS should be the pathname of
# the directory named `fer_dsets' you created to install the FERRET program.

# !!!! You must customize the following line:
# This directory will contain FERRET demonstration data files (30+ Mbytes)
	 setenv FER_DSETS /home/fiedler/ferret

# System Manager: Check this PATH modification for your system security.
# If you prefer not to modify PATH here you may comment out the following few
# lines and execute the file $FER_DIR/bin/install_ferret_links wich will
# create ferret links in /usr/local/bin.
# This logic will replace any previous $FER_DIR in PATH so this
# file may be sourced repeatedly in a single session
   if ( ! $?MYFERRET ) setenv MYFERRET $HOME/myferret
   if ( $PATH !~ *ferret* ) then
       if (${?prompt}) echo appending ferret path
       setenv PATH {$PATH}:$FER_DIR/bin
   else
#        echo replacing ferret path
      setenv PATH `echo $PATH | awk -F: '{for (i=1; i<=NF; i++) {if ($i \!~ /ferret\/bin/) {printf "%s:",$i}}}'`
      setenv PATH {$PATH}$FER_DIR/bin
   endif
# *** end of PATH modifications
if ( `hostname` == "nino" | `hostname` == "jedda" ) then
#  alias ferret ferret_v570
  endif

 
# =========== Initially make no modifications below this line ===========

#  Default Ferret document browser
   setenv FER_WEB_BROWSER "netscape -ncols 60"

   setenv FER_EXTERNAL_FUNCTIONS "$FER_DIR/ext_func/libs"

   setenv FER_GO    	". $MYFERRET/go $FER_DIR/go $FER_DIR/examples $FER_DIR/contrib"

   setenv FER_DATA  	". $FER_DSETS/data $FER_DIR/go $FER_DIR/examples $FER_DIR/contrib /home/lwrrdc/fiedler/etc /home/netcdf-data"
   setenv FER_DESCR 	". $FER_DSETS/descr /home/lwrrdc/fiedler/etc /home/blue1/fie048/fer_data"
   setenv FER_GRIDS 	". $FER_DSETS/grids"

   setenv TMAP      	  "$FER_DIR/fmt"
   setenv PLOTFONTS 	  "$FER_DIR/ppl/fonts"
   setenv SPECTRA   	  "$FER_DIR/ppl"         # for old ferret versions
   setenv FER_PALETTE 	  ". $MYFERRET/ppl $FER_DIR/ppl"       # palette search list

# SunOS uses shared libraries
if ( `uname` == SunOS ) then
#  IF USER DOES NOT HAVE LD_LIBRARY_PATH POINT TO X11
   if (! $?LD_LIBRARY_PATH) then
      setenv LD_LIBRARY_PATH "/usr/openwin/lib"
   endif
endif

# Mod for AIX Ferret users -- needed for namelist reads
if (`uname` == AIX) then
   setenv XLFRTEOPTS namelist=old
   setenv LANG C
endif

# Faddpath tool to add another path to the search lists quickly
#     usage: Faddpath new_path
alias Faddpath 'if \!$ != Faddpath setenv FER_GO "$FER_GO \!$"; if \!$ != Faddpath setenv FER_DATA "$FER_DATA \!$"; if \!$ != Faddpath setenv FER_DESCR "$FER_DESCR \!$"; if \!$ != Faddpath setenv FER_GRIDS "$FER_GRIDS \!$"'
